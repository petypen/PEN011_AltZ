
&НаКлиенте
Перем мЗакрытьФормуБезВопросов;

&НаКлиенте
Перем мТекСтрокаТаблицыРегистров;

&НаКлиенте
Перем мТекСтрокаТаблицыРегистровСтарая;

&НаКлиенте
Перем мПоследнийUUID;

&НаКлиенте
Перем мНеСсылочныеТипы;

&НаКлиенте
Перем мОбработкаВыбора;


&НаКлиенте
Процедура вПоказатьПредупреждение(ТекстСообщения)
	ПоказатьПредупреждение(, ТекстСообщения, 20);
КонецПроцедуры

&НаКлиенте
Процедура вПоказатьВопрос(ИмяПроцедуры, ТекстВопроса, ДопПараметры = Неопределено)
	ПоказатьВопрос(новый ОписаниеОповещения(ИмяПроцедуры, ЭтаФорма, ДопПараметры), ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, 20);
КонецПроцедуры


&НаСервере
Функция вПолучитьОбработку()
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

&НаСервереБезКонтекста
Функция вСкопироватьСтруктуру(Источник)
	Струк = новый Структура;
	
	Для каждого Элем из Источник Цикл
		Струк.Вставить(Элем.Ключ, Элем.Значение);
	КонецЦикла;
	
	Возврат Струк;
КонецФункции


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	_ОписаниеРазработки = Параметры.ОписаниеРазработки;
	
	мОбъектСсылка = Параметры.мОбъектСсылка;
	мОбъектСсылкаПредыдущий = Неопределено;
	Заголовок = Заголовок + " (" + _ОписаниеРазработки.НомерВерсии + " от " + _ОписаниеРазработки.ДатаВерсии + ")";
	
	ПутьКФормам = вПолучитьОбработку().Метаданные().ПолноеИмя() + ".Форма.";
	
	_ПоказыватьИтогиПоЧисловымКолонкамРД = ложь;
	_ИспользоватьЖирныйШрифтДляИтогов = истина;
	_ОсобыйРежимРаботыСНаборомЗаписейРБ = истина;
	
	Если Параметры.Свойство("Настройки") и ТипЗнч(Параметры.Настройки) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.Настройки);
	КонецЕсли;
	
	Если Параметры.Свойство("АдресаХранилищ") Тогда
		_АдресаХранилищ = вСкопироватьСтруктуру(Параметры.АдресаХранилищ);
	Иначе
		_АдресаХранилищ = новый Структура;
	КонецЕсли;
	
	Если не _АдресаХранилищ.Свойство("ОбщиеРеквизиты") Тогда
		_АдресаХранилищ.Вставить("ОбщиеРеквизиты", ПоместитьВоВременноеХранилище(-1, УникальныйИдентификатор));
	КонецЕсли;
	
	_БыстрыйВызовСервера = истина;
	_ПриЗаполненииОбрабатыватьТолькоВыделенныеСтроки = истина;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если мЗакрытьФормуБезВопросов = истина или _ЗадаватьВопросПриЗакрытии = ложь Тогда
		Возврат;
	КонецЕсли;
	
	Если _ТабРегистры.НайтиСтроки(новый Структура("Изменен", истина)).Количество() <> 0 Тогда
		Если ЗавершениеРаботы = Неопределено Тогда
			// для старых версии платформы
			Отказ = истина;
			вПоказатьВопрос("вЗакрытьФорму", "Редактор движений будет закрыт. Продолжить?");
			Возврат;
		КонецЕсли;
		
		Если ЗавершениеРаботы = ложь Тогда
			Отказ = истина;
			вПоказатьВопрос("вЗакрытьФорму", "Редактор движений будет закрыт. Продолжить?");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура вЗакрытьФорму(РезультатВопроса, ДопПараметры = Неопределено) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		мЗакрытьФормуБезВопросов = истина;
		ЭтаФорма.Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	мТекСтрокаТаблицыРегистров = Неопределено;
	мТекСтрокаТаблицыРегистровСтарая = Неопределено;
	
	мНеСсылочныеТипы = новый Массив;
	мНеСсылочныеТипы.Добавить(Тип("Дата"));
	мНеСсылочныеТипы.Добавить(Тип("Число"));
	мНеСсылочныеТипы.Добавить(Тип("Булево"));
	мНеСсылочныеТипы.Добавить(Тип("Строка"));
	мНеСсылочныеТипы.Добавить(Тип("ХранилищеЗначения"));
	мНеСсылочныеТипы.Добавить(Тип("УникальныйИдентификатор"));
	
	ПодключитьОбработчикОжидания("вПослеОткрытия", 0.1, истина);
КонецПроцедуры

&НаКлиенте
Процедура вПослеОткрытия() Экспорт
	_Обновить(Неопределено);
КонецПроцедуры


&НаКлиенте
Процедура мОбъектСсылкаПриИзменении(Элемент)
	ЭтаФорма.КлючУникальности = мОбъектСсылка;
	
	_Обновить(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура мОбъектСсылкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если мОбъектСсылка = Неопределено Тогда
		СтандартнаяОбработка = ложь;
		СтрукПарам = новый Структура("ЗакрыватьПриЗакрытииВладельца, ПереченьРазделов", истина, "Документы");
		ОткрытьФорму(ПутьКФормам + "ФормаВыбораОбъекта", СтрукПарам, Элемент,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		Массив = новый Массив;
		Массив.Добавить(ТипЗнч(мОбъектСсылка));
		Элемент.ОграничениеТипа = новый ОписаниеТипов(Массив);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура мОбъектСсылкаОчистка(Элемент, СтандартнаяОбработка)
	Элемент.ОграничениеТипа = новый ОписаниеТипов();
КонецПроцедуры



&НаКлиенте
Функция вПроверитьРегистратора()
	Если не ЗначениеЗаполнено(мОбъектСсылка) Тогда
		вПоказатьПредупреждение("Не задан объект для записи движений!");
		Возврат ложь;
	КонецЕсли;
	
	Возврат истина;
КонецФункции

&НаКлиенте
Процедура _ОткрытьВНовомОкне(Команда)
	СтрукПарам = новый Структура("ПутьКФормам, мОбъектСсылка, АдресаХранилищ, ОписаниеРазработки", ПутьКФормам, мОбъектСсылка, _АдресаХранилищ, _ОписаниеРазработки);
	ОткрытьФорму(ПутьКФормам + "ФормаРедакторДвижений",СтрукПарам,,ТекущаяДата(),,,,РежимОткрытияОкнаФормы.Независимый);
КонецПроцедуры

&НаКлиенте
Процедура _Обновить(Команда)
	мТекСтрокаТаблицыРегистров = Неопределено;
	мТекСтрокаТаблицыРегистровСтарая = Неопределено;
	
	вОчиститьНаборыЗаписей();
	
	вОбновить();
	
	Элементы.ГруппаРегистры.Заголовок = "Движения документа (" + _ТабРегистры.Количество() + ")";
КонецПроцедуры

&НаКлиенте
Процедура _Записать(Команда)
	Если не вПроверитьРегистратора() Тогда
		Возврат;
	КонецЕсли;
	
	Значение = _ТабРегистры.НайтиСтроки(новый Структура("Записать", истина)).Количество();
	Если Значение = 0 Тогда
		вПоказатьПредупреждение("Не отмечены регистры для записи.");
		Возврат;
	КонецЕсли;
	
	вПоказатьВопрос("_ЗаписатьДалее", СтрШаблон("Отмеченные регистры (%1 шт) будут записаны в базу. Продолжить?", Значение));
КонецПроцедуры

&НаКлиенте
Процедура _ЗаписатьДалее(РезультатВопроса, ДопПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		вЗаписать();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _СортироватьРегистрыСтандартно(Команда)
	_ТабРегистры.Сортировать("Изменен УБЫВ, Записать УБЫВ, ЧислоЗаписей УБЫВ, ПолноеИмя");
КонецПроцедуры

&НаКлиенте
Процедура _СнятьФлажки(Команда)
	Для каждого Стр из _ТабРегистры.НайтиСтроки(новый Структура("Записать", истина)) Цикл
		Стр.Записать = ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура _УстановитьФлажки(Команда)
	Для каждого Стр из _ТабРегистры.НайтиСтроки(новый Структура("Записать", ложь)) Цикл
		Стр.Записать = истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура _УстановитьФлажкиДляИзмененных(Команда)
	Для каждого Стр из _ТабРегистры.НайтиСтроки(новый Структура("Записать, Изменен", ложь, истина)) Цикл
		Стр.Записать = истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура _ОчиститьДвижения(Команда)
	Если не вПроверитьРегистратора() Тогда
		Возврат;
	КонецЕсли;
	
	Значение = Элементы._ТабРегистры.ВыделенныеСтроки;
	Если Значение.Количество() = 0 Тогда
		вПоказатьПредупреждение("Не отмечены регистры для очистки.");
		Возврат;
	КонецЕсли;
	
	вПоказатьВопрос("_ОчиститьДвиженияДалее", СтрШаблон("Выбранные регистры (%1 шт) будут очищены. Продолжить?", Значение.Количество()));
КонецПроцедуры

&НаКлиенте
Процедура _ОчиститьДвиженияДалее(РезультатВопроса, ДопПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыделенныеСтроки = Элементы._ТабРегистры.ВыделенныеСтроки;
		Для каждого Элем из ВыделенныеСтроки Цикл
			СтрДанные = _ТабРегистры.НайтиПоИдентификатору(Элем);
			Если СтрДанные <> Неопределено Тогда
				ИмяРеквизита = вПолучитьИмяРеквизита(СтрДанные.ПолноеИмя);
				
				Попытка
					ТабДанные = ЭтаФорма[ИмяРеквизита];
					Если ТабДанные.Количество() <> 0 Тогда
						ТабДанные.Очистить();
						СтрДанные.Записать = истина;
						СтрДанные.Изменен = истина;
						СтрДанные.ЕстьЗаписи = ложь;
						СтрДанные.ЧислоЗаписей = 0;
					КонецЕсли;
				Исключение
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры



&НаКлиенте
Процедура _ОбновитьНабор(Команда)
	ТекДанные = Элементы._ТабРегистры.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ИмяРеквизита = вПолучитьИмяРеквизита(ТекДанные.ПолноеИмя);
		
		Если _БыстрыйВызовСервера и _ПоказыватьИтогиПоЧисловымКолонкамРД = ложь Тогда
			Массив = вПрочитатьНаборЗаписейВКоллекцию(мОбъектСсылка, ТекДанные.ВидРегистра, ТекДанные.Имя, _ОсобыйРежимРаботыСНаборомЗаписейРБ);
			
			Коллекция = ЭтаФорма[ИмяРеквизита];
			Коллекция.Очистить();
			
			Для каждого Элем из Массив Цикл
				ЗаполнитьЗначенияСвойств(Коллекция.Добавить(), Элем);
			КонецЦикла;
		Иначе
			вОбновитьНаборЗаписей(ТекДанные.ВидРегистра, ТекДанные.Имя);
		КонецЕсли;
		
		ТекДанные.Изменен = ложь;
		ТекДанные.Записать = ложь;
		ТекДанные.ЧислоЗаписей = ЭтаФорма[ИмяРеквизита].Количество();
		ТекДанные.ЕстьЗаписи = (ТекДанные.ЧислоЗаписей <> 0);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _ЗаписатьНабор(Команда)
	Если не вПроверитьРегистратора() Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные = Элементы._ТабРегистры.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		вПоказатьПредупреждение("Не задан набор записей для сохранения");
		Возврат;
	КонецЕсли;
	вПоказатьВопрос("_ЗаписатьНаборДалее", "Набор записей будет записан в базу. Продолжить?");
КонецПроцедуры

&НаКлиенте
Процедура _ЗаписатьНаборДалее(РезультатВопроса, ДопПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ТекДанные = Элементы._ТабРегистры.ТекущиеДанные;
		Если ТекДанные <> Неопределено Тогда
			Если вЗаписатьНаборЗаписей(ТекДанные.ВидРегистра, ТекДанные.Имя) Тогда
				_ОбновитьНабор(Неопределено);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _ПереключитьАктивностьЗаписей(Команда)
	ТекДанные = Элементы._ТабРегистры.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ИмяРеквизита = вПолучитьИмяРеквизита(ТекДанные.ПолноеИмя);
		Если ЭтаФорма[ИмяРеквизита].Количество() <> 0 Тогда
			
			Для каждого Стр из ЭтаФорма[ИмяРеквизита] Цикл
				Стр.Активность = не Стр.Активность;
			КонецЦикла;
			
			НаборЗаписейПриИзменении(Элементы[ИмяРеквизита]);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _ОткрытьОбъект(Команда)
	ТекДанные = Элементы._ТабРегистры.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ИмяРеквизита = вПолучитьИмяРеквизита(ТекДанные.ПолноеИмя);
		ТекТаб = ЭтаФорма[ИмяРеквизита];
		
		Если ТекТаб.Количество() > 0 Тогда
			ТекТабЭФ = Элементы[ИмяРеквизита];
			ТекПолеЭФ = ТекТабЭФ.ТекущийЭлемент;
			
			пПоле = Сред(ТекПолеЭФ.Имя, СтрДлина(ИмяРеквизита)+2);
			Значение = ТекТабЭФ.ТекущиеДанные[пПоле];
			
			Если ЗначениеЗаполнено(Значение) Тогда
				
				Если ТипЗнч(Значение) = Тип("ХранилищеЗначения") Тогда
					вПоказатьЗначениеХЗ(Значение);
					
				ИначеЕсли вЭтоОбъектМетаданных(ТипЗнч(Значение)) Тогда
					СтрукПарам = новый Структура("ПутьКФормам, мОбъектСсылка, АдресаХранилищ", ПутьКФормам, Значение, _АдресаХранилищ);
					ОткрытьФорму(ПутьКФормам + "ФормаОбъекта", СтрукПарам, ,Значение);
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _ПоказатьТипЗначения(Команда)
	_ТипЗначенияТекущегоПоля = "";
	
	Значение = Неопределено;
	
	ТекДанные = Элементы._ТабРегистры.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ИмяРеквизита = вПолучитьИмяРеквизита(ТекДанные.ПолноеИмя);
		ТекТаб = ЭтаФорма[ИмяРеквизита];
		
		Если ТекТаб.Количество() > 0 Тогда
			ТекТабЭФ = Элементы[ИмяРеквизита];
			ТекПолеЭФ = ТекТабЭФ.ТекущийЭлемент;
			
			пПоле = Сред(ТекПолеЭФ.Имя, СтрДлина(ИмяРеквизита)+2);
			Значение = ТекТабЭФ.ТекущиеДанные[пПоле];
			
		КонецЕсли;
	КонецЕсли;
	
	Если Значение = Неопределено Тогда
		ИмяТипа = "Неопределено";
	Иначе
		ИмяТипа = вСформироватьИмяТипаПоЗначению(Значение);
	КонецЕсли;
	
	_ТипЗначенияТекущегоПоля = ИмяТипа;
КонецПроцедуры

&НаКлиенте
Процедура вПоказатьЗначениеХЗ(Значение)
	СтрукПарам = новый Структура("ПутьКФормам, ДанныеХЗ, АдресаХранилищ", ПутьКФормам, Значение, _АдресаХранилищ);
	ОткрытьФорму(ПутьКФормам + "ФормаОтображенияХЗ", СтрукПарам, ,ТекущаяДата());
КонецПроцедуры



&НаКлиентеНаСервереБезКонтекста
Функция вПолучитьИмяРеквизита(Знач ПолноеИмя)
	Возврат СтрЗаменить(ПолноеИмя, ".", "_");
КонецФункции

&НаКлиенте
Процедура вОчиститьНаборыЗаписей()
	Для каждого Стр из _ТабРегистры.НайтиСтроки(новый Структура("ЕстьРеквизитФормы", истина)) Цикл
		ИмяРеквизита = вПолучитьИмяРеквизита(Стр.ПолноеИмя);
		ЭтаФорма[ИмяРеквизита].Очистить();
		Стр.ЕстьЗаписи = ложь;
		Стр.ЧислоЗаписей = 0;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура вУдалитьРеквизитыНаборовЗаписей()
	МассивКСозданию = новый Массив;
	МассивКУдалению = новый Массив;
	
	Для каждого Стр из _ТабРегистры.НайтиСтроки(новый Структура("ЕстьРеквизитФормы", истина)) Цикл
		ИмяРеквизита = вПолучитьИмяРеквизита(Стр.ПолноеИмя);
		
		Если вПроверитьНаличиеРеквизита(ИмяРеквизита) Тогда
			МассивКУдалению.Добавить(ИмяРеквизита);
		КонецЕсли;
		Стр.ЕстьРеквизитФормы = ложь;
		
		ЭФ = Элементы.Найти("Стр_" + ИмяРеквизита);
		Если ЭФ <> Неопределено Тогда
			Элементы.Удалить(ЭФ);
		КонецЕсли;
	КонецЦикла;
	
	ИзменитьРеквизиты(МассивКСозданию, МассивКУдалению);
КонецПроцедуры

&НаСервереБезКонтекста
Функция вЭтоОбъектМетаданных(Знач Тип)
	ОбъектМД = Метаданные.НайтиПоТипу(Тип);
	Возврат ( ОбъектМД <> Неопределено и не Метаданные.Перечисления.Содержит(ОбъектМД) );
КонецФункции

&НаСервереБезКонтекста
Функция вСформироватьИмяТипаПоЗначению(Знач Значение)
	пТип = ТипЗнч(Значение);
	
	ОбъектМД = Метаданные.НайтиПоТипу(пТип);
	Если ОбъектМД <> Неопределено Тогда
		ИмяТипа = ОбъектМД.ПолноеИмя();
	Иначе
		ИмяТипа = Строка(пТип);
	КонецЕсли;
	
	Возврат ИмяТипа;
КонецФункции

&НаСервереБезКонтекста
Функция вПрочитатьНаборЗаписей(Знач Регистратор, Знач ВидРегистра, Знач ИмяРегистра, Знач пОсобыйРежимРаботыСНаборомЗаписейРБ)
	Набор = вСоздатьНаборЗаписей(Регистратор, ВидРегистра, ИмяРегистра);
	Набор.Прочитать();
	
	ТабРезультат = Набор.Выгрузить();
	
	Если пОсобыйРежимРаботыСНаборомЗаписейРБ Тогда
		вНастроитьТаблицуНабораЗаписей(Набор, ТабРезультат);
	КонецЕсли;
	
	Возврат ТабРезультат;
КонецФункции

&НаСервереБезКонтекста
Процедура вНастроитьТаблицуНабораЗаписей(пНаборЗаписей, пТабРезультат)
	Если пТабРезультат = Неопределено Тогда
		пТабРезультат = новый ТаблицаЗначений;
	КонецЕсли;
	
	пОбъектМД = пНаборЗаписей.Метаданные();
	Если Метаданные.РегистрыБухгалтерии.Содержит(пОбъектМД) Тогда
		пКолонкиДляОбработки = "";
		Для каждого Элем из пОбъектМД.Измерения Цикл
			Если Элем.ПризнакУчета <> Неопределено Тогда
				пСуффиксы = новый Массив;
				Если Элем.Балансовый или не пОбъектМД.Корреспонденция Тогда
					пСуффиксы.Добавить("");
				Иначе
					пСуффиксы.Добавить("Дт");
					пСуффиксы.Добавить("Кт");
				КонецЕсли;
				
				Для каждого пСуфф из пСуффиксы Цикл
					пИмяКолонки = Элем.Имя + пСуфф;
					пСтараяКолонка = пТабРезультат.Колонки.Найти(пИмяКолонки);
					Если пСтараяКолонка <> Неопределено Тогда
						пКолонкиДляОбработки = пКолонкиДляОбработки + "," + пИмяКолонки;
						пТипЗначения = новый ОписаниеТипов(пСтараяКолонка.ТипЗначения, "NULL");
						пНоваяКолонка = пТабРезультат.Колонки.Вставить(пТабРезультат.Колонки.Индекс(пСтараяКолонка), , пТипЗначения, пСтараяКолонка.Заголовок, пСтараяКолонка.Ширина);
						пТабРезультат.Колонки.Удалить(пСтараяКолонка);
						пНоваяКолонка.Имя = пИмяКолонки;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		Если не ПустаяСтрока(пКолонкиДляОбработки) Тогда
			пКолонкиДляОбработки = Сред(пКолонкиДляОбработки, 2);
			
			Сч = -1;
			Для каждого Стр из пНаборЗаписей Цикл
				Сч = Сч + 1;
				ЗаполнитьЗначенияСвойств(пТабРезультат[Сч], Стр, пКолонкиДляОбработки);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция вСоздатьНаборЗаписей(Регистратор, ВидРегистра, ИмяРегистра)
	Если ВидРегистра = "РегистрСведений" Тогда
		Менеджер = РегистрыСведений[ИмяРегистра];
	ИначеЕсли ВидРегистра = "РегистрНакопления" Тогда
		Менеджер = РегистрыНакопления[ИмяРегистра];
	ИначеЕсли ВидРегистра = "РегистрРасчета" Тогда
		Менеджер = РегистрыРасчета[ИмяРегистра];
	ИначеЕсли ВидРегистра = "РегистрБухгалтерии" Тогда
		Менеджер = РегистрыБухгалтерии[ИмяРегистра];
	Иначе
		Менеджер = Неопределено;
	КонецЕсли;
	
	Набор = Менеджер.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(Регистратор);
	
	Возврат Набор;
КонецФункции

&НаСервереБезКонтекста
Функция вПрочитатьНаборЗаписейВКоллекцию(Знач Регистратор, Знач ВидРегистра, Знач ИмяРегистра, Знач пОсобыйРежимРаботыСНаборомЗаписейРБ)
	ТабРезультат = вПрочитатьНаборЗаписей(Регистратор, ВидРегистра, ИмяРегистра, пОсобыйРежимРаботыСНаборомЗаписейРБ);
	
	Струк = новый Структура;
	
	Для каждого Элем из ТабРезультат.Колонки Цикл
		Струк.Вставить(Элем.Имя);
	КонецЦикла;
	
	Массив = новый Массив;
	
	Для каждого Стр из ТабРезультат Цикл
		НС = новый Структура;
		Для каждого Элем из Струк Цикл
			НС.Вставить(Элем.Ключ);
		КонецЦикла;
		
		ЗаполнитьЗначенияСвойств(НС, Стр);
		Массив.Добавить(НС);
	КонецЦикла;
	
	Возврат Массив;
КонецФункции

&НаСервере
Процедура вОбновитьНаборЗаписей(Знач ВидРегистра, Знач ИмяРегистра)
	ТабРезультат = вПрочитатьНаборЗаписей(мОбъектСсылка, ВидРегистра, ИмяРегистра, _ОсобыйРежимРаботыСНаборомЗаписейРБ);
	ЗначениеВРеквизитФормы(ТабРезультат, вПолучитьИмяРеквизита(ВидРегистра + "." + ИмяРегистра));
	
	Если _ПоказыватьИтогиПоЧисловымКолонкамРД Тогда
		вПересчитатьИтогиПоКолонкам(ВидРегистра + "." + ИмяРегистра, ТабРезультат);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция вЗаписатьНаборЗаписей(Знач ВидРегистра, Знач ИмяРегистра)
	Попытка
		ИмяРеквизита = вПолучитьИмяРеквизита(ВидРегистра + "." + ИмяРегистра);
		Набор = вСоздатьНаборЗаписей(мОбъектСсылка, ВидРегистра, ИмяРегистра);
		
		Если ЭтаФорма[ИмяРеквизита].Количество() <> 0 Тогда
			ТабРезультат = РеквизитФормыВЗначение(ИмяРеквизита);
			Набор.Загрузить(ТабРезультат);
		КонецЕсли;
		
		Если _ЗаписьВРежимеЗагрузки Тогда
			Набор.ОбменДанными.Загрузка = истина;
		КонецЕсли;
		Набор.Записать();
	Исключение
		Сообщить(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат ложь;
	КонецПопытки;
	
	Возврат истина;
КонецФункции

&НаСервере
Функция вЗаписать()
	НайденныеСтроки = _ТабРегистры.НайтиСтроки(новый Структура("Записать", истина));
	пЕстьТранзакция = (НайденныеСтроки.Количество() > 1);
	
	Если пЕстьТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;
	
	Для каждого Стр из НайденныеСтроки Цикл
		Если не вЗаписатьНаборЗаписей(Стр.ВидРегистра, Стр.Имя) Тогда
			Если пЕстьТранзакция Тогда
				ОтменитьТранзакцию();
				Возврат ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если пЕстьТранзакция Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
	Для каждого Стр из НайденныеСтроки Цикл
		ИмяРеквизита = вПолучитьИмяРеквизита(Стр.ПолноеИмя);
		
		вОбновитьНаборЗаписей(Стр.ВидРегистра, Стр.Имя);
		
		Стр.Изменен = ложь;
		Стр.Записать = ложь;
		Стр.ЧислоЗаписей = ЭтаФорма[ИмяРеквизита].Количество();
		Стр.ЕстьЗаписи = (Стр.ЧислоЗаписей <> 0);
	КонецЦикла;
	
	Возврат истина;
КонецФункции


&НаСервереБезКонтекста
Функция вОписаниеТиповДляUUID(пОписаниеТипов)
	Если пОписаниеТипов.Типы().Количество() = 1 Тогда
		пНовоеОписаниеТипов = новый ОписаниеТипов(пОписаниеТипов, "Строка");
	Иначе
		пНовоеОписаниеТипов = пОписаниеТипов;
	КонецЕсли;
	
	Возврат пНовоеОписаниеТипов;
КонецФункции

&НаСервере
Процедура вСоздатьРеквизитыНаборовЗаписей(НадоСоздаватьРеквизиты = истина)
	ТипХЗ = Тип("ХранилищеЗначения");
	ТипТТ = Тип("Тип");
	ТипМВ = Тип("МоментВремени");
	ТипUUID = Тип("УникальныйИдентификатор");
	
	пТипЧисло = Тип("Число");
	
	СоотвДанные = новый Соответствие;
	
	МассивКСозданию = новый Массив;
	МассивКУдалению = новый Массив;
	
	Для каждого Стр из _ТабРегистры Цикл
		Стр.ЕстьРеквизитФормы = истина;
		
		ИмяРеквизита = вПолучитьИмяРеквизита(Стр.ПолноеИмя);
		
		Если НадоСоздаватьРеквизиты Тогда
			МассивКСозданию.Добавить(новый РеквизитФормы(ИмяРеквизита, новый ОписаниеТипов("ТаблицаЗначений"), , Стр.ПолноеИмя, ложь));
		КонецЕсли;
		
		ТабРезультат = вПрочитатьНаборЗаписей(мОбъектСсылка, Стр.ВидРегистра, Стр.Имя, _ОсобыйРежимРаботыСНаборомЗаписейРБ);
		СоотвДанные.Вставить(ИмяРеквизита, ТабРезультат);
		
		Стр.ЧислоЗаписей = ТабРезультат.Количество();
		Стр.ЕстьЗаписи = (Стр.ЧислоЗаписей <> 0);
		Стр.Изменен = ложь;
		Стр.Записать = ложь;
		
		Если НадоСоздаватьРеквизиты Тогда
			Для каждого Колонка из ТабРезультат.Колонки Цикл
				Если Колонка.ТипЗначения.СодержитТип(ТипХЗ) Тогда
					ТипЗначенияРеквизита = новый ОписаниеТипов;
				ИначеЕсли Колонка.ТипЗначения.СодержитТип(ТипТТ) Тогда
					ТипЗначенияРеквизита = новый ОписаниеТипов;
				ИначеЕсли Колонка.ТипЗначения.СодержитТип(ТипМВ) Тогда
					ТипЗначенияРеквизита = новый ОписаниеТипов;
				ИначеЕсли Колонка.ТипЗначения.СодержитТип(ТипUUID) Тогда
					ТипЗначенияРеквизита = вОписаниеТиповДляUUID(Колонка.ТипЗначения);
				Иначе
					ТипЗначенияРеквизита = Колонка.ТипЗначения;
				КонецЕсли;
				МассивКСозданию.Добавить(новый РеквизитФормы(Колонка.Имя, ТипЗначенияРеквизита, ИмяРеквизита, Колонка.Заголовок, ложь));
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НадоСоздаватьРеквизиты Тогда
		ИзменитьРеквизиты(МассивКСозданию, МассивКУдалению);
	КонецЕсли;
	
	_ТабРегистры.Сортировать("Изменен УБЫВ, ЧислоЗаписей УБЫВ, ПолноеИмя");
	
	// создание элементов формы
	СтрукСпецКолонки = новый Структура("Регистратор, МоментВремени");
	
	Для каждого ЭлемХ из СоотвДанные Цикл
		ТабРезультат = ЭлемХ.Значение;
		ИмяРеквизита = ЭлемХ.Ключ;
		
		ЗначениеВРеквизитФормы(ЭлемХ.Значение, ЭлемХ.Ключ);
		
		Если не НадоСоздаватьРеквизиты Тогда
			Если _ПоказыватьИтогиПоЧисловымКолонкамРД Тогда
				вПересчитатьИтогиПоКолонкам(ИмяРеквизита, ТабРезультат);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		НоваяСтраница = Элементы.Добавить("Стр_" + ИмяРеквизита, Тип("ГруппаФормы"), Элементы.СтраницыНаборыЗаписей);
		НоваяСтраница.Вид = ВидГруппыФормы.Страница;
		НоваяСтраница.Заголовок = "";
		НоваяСтраница.Видимость = истина;
		
		ЭлемТЗ = ЭтаФорма.Элементы.Добавить(ИмяРеквизита, Тип("ТаблицаФормы"), НоваяСтраница);
		ЭлемТЗ.ПутьКДанным = ИмяРеквизита;
		ЭлемТЗ.УстановитьДействие("ПриИзменении", "НаборЗаписейПриИзменении");
		
		Элем = ЭтаФорма.Элементы.Добавить("_" + ИмяРеквизита + "_ПереключитьАктивностьЗаписей", Тип("КнопкаФормы"), ЭлемТЗ.КоманднаяПанель);
		Элем.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		Элем.ИмяКоманды = "_ПереключитьАктивностьЗаписей";
		
		Элем = ЭтаФорма.Элементы.Добавить("_" + ИмяРеквизита + "_ОткрытьОбъект", Тип("КнопкаФормы"), ЭлемТЗ.КоманднаяПанель);
		Элем.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		Элем.ИмяКоманды = "_ОткрытьОбъект";
		
		Элем = ЭтаФорма.Элементы.Добавить("_" + ИмяРеквизита + "_ОбновитьНабор", Тип("КнопкаФормы"), ЭлемТЗ.КоманднаяПанель);
		Элем.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		Элем.ИмяКоманды = "_ОбновитьНабор";
		
		Элем = ЭтаФорма.Элементы.Добавить("_" + ИмяРеквизита + "_ЗаписатьНабор", Тип("КнопкаФормы"), ЭлемТЗ.КоманднаяПанель);
		Элем.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		Элем.ИмяКоманды = "_ЗаписатьНабор";
		
		ГруппаКнопок = Элементы.Добавить("Группа_" + ЭлемТЗ.Имя, Тип("ГруппаФормы"), ЭлемТЗ.КонтекстноеМеню);
		ГруппаКнопок.Вид = ВидГруппыФормы.ГруппаКнопок;
				
		Кнопка = Элементы.Добавить("_ОткрытьСпецФормуВыбора_" + ЭлемТЗ.Имя, Тип("КнопкаФормы"), ГруппаКнопок);
		Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		Кнопка.ИмяКоманды = "_ОткрытьСпецФормуВыбора";
		
		Кнопка = Элементы.Добавить("_ВставитьСсылкуНаОбъект_" + ЭлемТЗ.Имя, Тип("КнопкаФормы"), ГруппаКнопок);
		Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		Кнопка.ИмяКоманды = "_ВставитьСсылкуНаОбъект";
		
		Кнопка = Элементы.Добавить("_ВставитьУникальныйИдентификатор_" + ЭлемТЗ.Имя, Тип("КнопкаФормы"), ГруппаКнопок);
		Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		Кнопка.ИмяКоманды = "_ВставитьУникальныйИдентификатор";
		
		пЕстьЧисловыеКолонки = ложь;
		
		Для каждого Колонка из ТабРезультат.Колонки Цикл
			Если СтрукСпецКолонки.Свойство(Колонка.Имя) Тогда
				Продолжить;
			КонецЕсли;
			
			Элем = ЭтаФорма.Элементы.Добавить(ИмяРеквизита + "_" + Колонка.Имя, Тип("ПолеФормы"), ЭлемТЗ);
			Элем.ПутьКДанным = ИмяРеквизита + "." + Колонка.Имя;
			Элем.Вид = ВидПоляФормы.ПолеВвода;
			Элем.ДоступныеТипы = Колонка.ТипЗначения;
			Элем.КнопкаОчистки = истина;
			
			Если Колонка.ТипЗначения.СодержитТип(ТипХЗ) Тогда // версия 033
				Элем.ТолькоПросмотр = истина;
			Иначе
				Элем.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
			КонецЕсли;
			
			Если _ПоказыватьИтогиПоЧисловымКолонкамРД и Колонка.ТипЗначения.СодержитТип(пТипЧисло) и Колонка.Имя <> "НомерСтроки" Тогда
				пЕстьЧисловыеКолонки = истина;
				Элем.ГоризонтальноеПоложениеВПодвале = ГоризонтальноеПоложениеЭлемента.Право;
				Элем.ЦветТекстаПодвала = ЦветаСтиля.ЦветФонаВыделенияПоля;
				Если _ИспользоватьЖирныйШрифтДляИтогов Тогда
					Элем.ШрифтПодвала = новый Шрифт(Элем.Шрифт,,,истина);
				КонецЕсли;
				Элем.ТекстПодвала = Строка(ТабРезультат.Итог(Колонка.Имя));
			КонецЕсли;
		КонецЦикла;
		
		Если _ПоказыватьИтогиПоЧисловымКолонкамРД и пЕстьЧисловыеКолонки Тогда
			ЭлемТЗ.Подвал = истина;
			Элем = ЭтаФорма.Элементы.Добавить("_" + ИмяРеквизита + "_ПересчитатьИтогиПоКолонкам", Тип("КнопкаФормы"), ЭлемТЗ.КоманднаяПанель);
			Элем.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
			Элем.ИмяКоманды = "_ПересчитатьИтогиПоКолонкам";
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция вПроверитьНаличиеРеквизита(ИмяРеквизита)
	Струк = новый Структура(ИмяРеквизита);
	ЗаполнитьЗначенияСвойств(Струк, ЭтаФорма);
	
	Возврат (Струк[ИмяРеквизита] <> Неопределено);
КонецФункции

&НаСервере
Процедура вОбновить()
	НадоСоздаватьРеквизиты = (ТипЗнч(мОбъектСсылка) <> ТипЗнч(мОбъектСсылкаПредыдущий));
	
	мОбъектСсылкаПредыдущий = мОбъектСсылка;
	
	Если НадоСоздаватьРеквизиты Тогда
		вУдалитьРеквизитыНаборовЗаписей();
		
		_ТабРегистры.Очистить();
		
		мТекСтрокаТаблицыРегистров = Неопределено;
		
		Если мОбъектСсылка <> Неопределено Тогда
			ОбъектМД = мОбъектСсылка.Метаданные();
			_ПолноеИмяДокумента = ОбъектМД.ПолноеИмя();
			
			пКэшИмен = новый Соответствие;
			
			Для каждого ОбъектРегистрМД из ОбъектМД.Движения Цикл
				пПолноеИмяРегистра = ОбъектРегистрМД.ПолноеИмя();
				
				Если пКэшИмен[пПолноеИмяРегистра] = Неопределено Тогда
					//!!! защита от кривых конфигураций
					пКэшИмен[пПолноеИмяРегистра] = 1;
					
					НС = _ТабРегистры.Добавить();
					НС.Имя = ОбъектРегистрМД.Имя;
					НС.Представление = ОбъектРегистрМД.Представление();
					НС.ПолноеИмя = пПолноеИмяРегистра;
					НС.ВидРегистра = Лев(НС.ПолноеИмя, СтрНайти(НС.ПолноеИмя, ".")-1);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		_ТабРегистры.Сортировать("ПолноеИмя");
	КонецЕсли;
	
	вСоздатьРеквизитыНаборовЗаписей(НадоСоздаватьРеквизиты);
КонецПроцедуры


&НаКлиенте
Процедура НаборЗаписейПриИзменении(Элемент)
	ТекДанные = Элементы._ТабРегистры.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ТекДанные.Изменен = истина;
		ТекДанные.Записать = истина;
		ТекДанные.ЧислоЗаписей = ЭтаФорма[Элемент.Имя].Количество();
		ТекДанные.ЕстьЗаписи = (ТекДанные.ЧислоЗаписей <> 0);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _ТабРегистрыПриАктивизацииСтроки(Элемент)
	ТекСтрока = Элемент.ТекущаяСтрока;
	Если ТекСтрока <> мТекСтрокаТаблицыРегистров Тогда
		мТекСтрокаТаблицыРегистровСтарая = мТекСтрокаТаблицыРегистров;
		мТекСтрокаТаблицыРегистров = ТекСтрока;
		ПодключитьОбработчикОжидания("вПриАктивизацииСтрокиТаблицыРегистров", 0.1, истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура вПриАктивизацииСтрокиТаблицыРегистров() Экспорт
	Если мЗакрытьФормуБезВопросов = истина Тогда
		Возврат;
	КонецЕсли;
	
	Если мТекСтрокаТаблицыРегистровСтарая <> Неопределено Тогда
		ТекДанные = _ТабРегистры.НайтиПоИдентификатору(мТекСтрокаТаблицыРегистровСтарая);
		Если ТекДанные <> Неопределено Тогда
			ИмяРеквизита = вПолучитьИмяРеквизита(ТекДанные.ПолноеИмя);
		КонецЕсли;
	КонецЕсли;
	
	ТекДанные = Неопределено;
	Если мТекСтрокаТаблицыРегистров <> Неопределено Тогда
		ТекДанные = _ТабРегистры.НайтиПоИдентификатору(мТекСтрокаТаблицыРегистров);
		Если ТекДанные <> Неопределено Тогда
			ИмяРеквизита = вПолучитьИмяРеквизита(ТекДанные.ПолноеИмя);
			пТекСтраница = Элементы.Найти("Стр_" + ИмяРеквизита);
			Если пТекСтраница <> Неопределено Тогда
 				Элементы.СтраницыНаборыЗаписей.ТекущаяСтраница = пТекСтраница;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ТекДанные = Неопределено Тогда
		Элементы.СтраницыНаборыЗаписей.ТекущаяСтраница = Элементы.СтрПример;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура _ЗаполнитьДанныеТекущейКолонки(Команда)
	ТекДанные = Элементы._ТабРегистры.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ИмяРеквизита = вПолучитьИмяРеквизита(ТекДанные.ПолноеИмя);
		ТекТаб = ЭтаФорма[ИмяРеквизита];
		
		Если ТекТаб.Количество() > 0 Тогда
			
			пЗначение = _ЗначениеДляЗаполнения;
			
			ТекДанные.Записать = истина;
			ТекДанные.Изменен = истина;
			
			ТекТабЭФ = Элементы[ИмяРеквизита];
			ТекПолеЭФ = ТекТабЭФ.ТекущийЭлемент;
			
			пПоле = Сред(ТекПолеЭФ.Имя, СтрДлина(ИмяРеквизита)+2);
			
			Если _ПриЗаполненииОбрабатыватьТолькоВыделенныеСтроки Тогда
				Для каждого Элем из ТекТабЭФ.ВыделенныеСтроки Цикл
					Стр = ТекТаб.НайтиПоИдентификатору(Элем);
					Стр[пПоле] = пЗначение;
				КонецЦикла;
			Иначе
				Для каждого Стр из ТекТаб Цикл
					Стр[пПоле] = пЗначение;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _ЗначениеДляЗаполненияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если _ЗначениеДляЗаполнения = NULL Тогда
		СтандартнаяОбработка = ложь;
		ПоказатьПредупреждение(, "Текущее значение NULL", 20);
	ИначеЕсли _ЗначениеДляЗаполнения = Неопределено Тогда
		СтандартнаяОбработка = ложь;
		СтрукПарам = новый Структура("ЗакрыватьПриЗакрытииВладельца, ТипыДляЗаполненияЗначений", истина, истина);
		ОткрытьФорму(ПутьКФормам + "ФормаВыбораОбъекта", СтрукПарам, Элемент,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ИначеЕсли ТипЗнч(_ЗначениеДляЗаполнения) = Тип("УникальныйИдентификатор") Тогда
		СтандартнаяОбработка = ложь;
	Иначе
		Массив = новый Массив;
		Массив.Добавить(ТипЗнч(_ЗначениеДляЗаполнения));
		Элемент.ОграничениеТипа = новый ОписаниеТипов(Массив);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _ЗначениеДляЗаполненияОчистка(Элемент, СтандартнаяОбработка)
	Элемент.ОграничениеТипа = новый ОписаниеТипов();
КонецПроцедуры


// загрузка движений из другого документа
&НаСервереБезКонтекста
Функция вПолучитьПериодРегистрации(ДокСсылка, ИмяТаблицы)
	Запрос = новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	АвансовыйОтчет.Дата КАК Дата
	|ИЗ
	|	" + ИмяТаблицы + " КАК АвансовыйОтчет
	|ГДЕ
	|	АвансовыйОтчет.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат ?(Выборка.Следующий(), Выборка.Дата, Неопределено);
КонецФункции


&НаКлиенте
Процедура _ЗагрузитьДвиженияДругогоДокумента(Команда)
	Если не вПроверитьРегистратора() Тогда
		Возврат;
	КонецЕсли;
	
	СтрукПарам = новый Структура("ЗакрыватьПриЗакрытииВладельца, ПереченьРазделов", истина, "Документы");
	ОткрытьФорму(ПутьКФормам + "ФормаВыбораОбъекта", СтрукПарам, ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ТипЗнч(ИсточникВыбора) = Тип("ФормаКлиентскогоПриложения") и СтрНайти(ИсточникВыбора.ИмяФормы, "Форма.ФормаСпискаОбъектов") <> 0 Тогда
		ОбработкаВыбораСпец(ВыбранноеЗначение, ИсточникВыбора);
		Возврат;
	КонецЕсли;
	
	ПоказатьВводЗначения(новый ОписаниеОповещения("вОбработатьВыборДокументаДляЗагрузкиДвижений", ЭтаФорма), ВыбранноеЗначение, "Документ для загрузки движений");
КонецПроцедуры

&НаКлиенте
Процедура вОбработатьВыборДокументаДляЗагрузкиДвижений(Значение, ДопПараметры = Неопределено) Экспорт
	Если Значение <> Неопределено Тогда
		вПоказатьВопрос("вЗагрузитьДвиженияИзДокумента", "Будут загружены движения из выбранного документа. Продолжить?", Значение);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура вЗагрузитьДвиженияИзДокумента(РезультатВопроса, ДокСсылка) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да и ЗначениеЗаполнено(ДокСсылка) Тогда
		Если вЗагрузитьДвиженияИзДокументаНаСервере(ДокСсылка) Тогда
			_СортироватьРегистрыСтандартно(Неопределено);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция вЗагрузитьДвиженияИзДокументаНаСервере(ДокСсылка)
	пРезультат = ложь;
	
	пДжвиженияПриемник = мОбъектСсылка.Метаданные().Движения;
	
	Для каждого ЭлеметМД из ДокСсылка.Метаданные().Движения Цикл
		Если пДжвиженияПриемник.Содержит(ЭлеметМД) Тогда
			пПолноеИмя = ЭлеметМД.ПолноеИмя();
			пВидРегистра = Лев(пПолноеИмя, СтрНайти(пПолноеИмя, ".")-1);
			
			ТабДанные = вПрочитатьНаборЗаписей(ДокСсылка, пВидРегистра, ЭлеметМД.Имя, _ОсобыйРежимРаботыСНаборомЗаписейРБ);
			Если ТабДанные.Количество() <> 0 Тогда
				СтрукОсновное = новый Структура("Период, Регистратор", вПолучитьПериодРегистрации(мОбъектСсылка, _ПолноеИмяДокумента), мОбъектСсылка);
				ИмяРеквизита = вПолучитьИмяРеквизита(пПолноеИмя);
				ТабНабор = ЭтаФорма[ИмяРеквизита];
				Для каждого Стр из ТабДанные Цикл
					НС = ТабНабор.Добавить();
					ЗаполнитьЗначенияСвойств(НС, Стр);
					ЗаполнитьЗначенияСвойств(НС, СтрукОсновное);
				КонецЦикла;
				
				Массив = _ТабРегистры.НайтиСтроки(новый Структура("ПолноеИмя", пПолноеИмя));
				Если Массив.Количество() <> 0 Тогда
					СтрРегистр = Массив[0];
					СтрРегистр.Записать = истина;
					СтрРегистр.Изменен = истина;
					СтрРегистр.ЕстьЗаписи = истина;
					СтрРегистр.ЧислоЗаписей = ТабНабор.Количество();
				КонецЕсли;
				
				пРезультат = истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат пРезультат;
КонецФункции


&НаКлиенте
Процедура _ВставитьУникальныйИдентификатор(Команда)
	ТекТаблица = ЭтаФорма.ТекущийЭлемент;
	
	Если ТекТаблица.Имя = "_ЗначениеДляЗаполнения" Тогда
		пСтрук = новый Структура("Таблица", ТекТаблица.Имя);
		ПоказатьВводСтроки(новый ОписаниеОповещения("вОбработатьВвод_UUID", ЭтаФорма, пСтрук),мПоследнийUUID,"Введите уникальный идентификатор",,ложь);
		Возврат;
	ИначеЕсли ТипЗнч(ТекТаблица) <> Тип("ТаблицаФормы") Тогда
		Возврат;
	КонецЕсли;
	
	ТекКолонка = ТекТаблица.ТекущийЭлемент;
	Если ТекКолонка = Неопределено или ТекКолонка.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		пДоступныеТипы = ТекКолонка.ДоступныеТипы.Типы();
		Если пДоступныеТипы.Количество() <> 0 и пДоступныеТипы.Найти(Тип("УникальныйИдентификатор")) <> 0 Тогда
			Возврат;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	ТекДанные = Элементы[ТекТаблица.Имя].ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		пСтрук = новый Структура("Таблица", ТекТаблица.Имя);
		
		пСтрук.Вставить("Поле", Сред(ТекКолонка.Имя, СтрДлина(ТекТаблица.Имя)+2));
		
		ПоказатьВводСтроки(новый ОписаниеОповещения("вОбработатьВвод_UUID", ЭтаФорма, пСтрук),мПоследнийUUID,"Введите уникальный идентификатор",,ложь);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура вОбработатьВвод_UUID(Строка, пСтрук = Неопределено) Экспорт
	Если Строка <> Неопределено и не ПустаяСтрока(Строка) Тогда
		Попытка
			пЗначение = новый УникальныйИдентификатор(Строка);
			мПоследнийUUID = Строка;
		Исключение
			ПоказатьПредупреждение(, "Значение не может быть преобразовано в Уникальный идентификатор!", 20);
			Возврат;
		КонецПопытки;
		
		Если пСтрук.Таблица = "_ЗначениеДляЗаполнения" Тогда
			_ЗначениеДляЗаполнения = пЗначение;
		Иначе
			ТекДанные = Элементы[пСтрук.Таблица].ТекущиеДанные;
			Если ТекДанные <> Неопределено Тогда
				ТекДанные[пСтрук.Поле] = пЗначение;
				
				ТекДанныеРег = Элементы._ТабРегистры.ТекущиеДанные;
				ТекДанныеРег.Записать = истина;
				ТекДанныеРег.Изменен = истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


// ИТОГИ ПО ЧИСЛОВЫМ КОЛОНКАМ

&НаКлиенте
Процедура _ПересчитатьИтогиПоКолонкам(Команда)
	Если _ПоказыватьИтогиПоЧисловымКолонкамРД Тогда
		ТекДанные = Элементы._ТабРегистры.ТекущиеДанные;
		Если ТекДанные <> Неопределено Тогда
			вПересчитатьИтогиПоКолонкам(ТекДанные.ПолноеИмя);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура вПересчитатьИтогиПоКолонкам(Знач пПолноеИмя, Знач пТабРезультат = Неопределено)
	пТипЧисло = Тип("Число");
	
	пИмяРеквизита = вПолучитьИмяРеквизита(пПолноеИмя);
	
	Если пТабРезультат = Неопределено Тогда
		пТабРезультат = РеквизитФормыВЗначение(пИмяРеквизита);
	КонецЕсли;
	
	Для каждого пКолонка из пТабРезультат.Колонки Цикл
		Если пКолонка.ТипЗначения.СодержитТип(пТипЧисло) и пКолонка.Имя <> "НомерСтроки" Тогда
			ЭФ = Элементы.Найти(пИмяРеквизита + "_" + пКолонка.Имя);
			Если ЭФ <> Неопределено Тогда
				ЭФ.ТекстПодвала = Строка(пТабРезультат.Итог(пКолонка.Имя));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


// СПЕЦИАЛЬНАЯ ФОРМА ВЫБОРА ОБЪЕКТА ИЗ СПИСКА
&НаКлиенте
Процедура _ОткрытьСпецФормуВыбора(Команда)
	ТекТаблица = ЭтаФорма.ТекущийЭлемент;
	
	Если ТекТаблица.Имя = "_ЗначениеДляЗаполнения" Тогда
		ЭФ = ТекТаблица;
		
		Если _ЗначениеДляЗаполнения = Неопределено Тогда
			
			СтрукПарам = новый Структура("ЗакрыватьПриЗакрытииВладельца, ТипыДляЗаполненияЗначений", истина, истина);
			ОткрытьФорму(ПутьКФормам + "ФормаВыбораОбъекта", СтрукПарам, ЭФ,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		Иначе
			
			пПолноеИмя = вПолучитьПолноеИмяМД(_ЗначениеДляЗаполнения);
			Если пПолноеИмя = Неопределено Тогда
				Возврат;
			КонецЕсли;
			
			СтрукПараметры = новый Структура("ПутьКФормам, ПолноеИмя, АдресаХранилищ", ПутьКФормам, пПолноеИмя, _АдресаХранилищ);
			СтрукПараметры.Вставить("ТекущаяСтрока", _ЗначениеДляЗаполнения);
			СтрукПараметры.Вставить("РежимВыбора", истина);
			Попытка
				ОткрытьФорму(ПутьКФормам + "ФормаСпискаОбъектов", СтрукПараметры, ЭФ,истина,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			Исключение
				пОписаниеОшибки = ОписаниеОшибки();
			КонецПопытки;
			
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
		
	Если ТипЗнч(ТекТаблица) <> Тип("ТаблицаФормы") Тогда
		Возврат;
	КонецЕсли;
	
	ТекКолонка = ТекТаблица.ТекущийЭлемент;
	Если ТекКолонка = Неопределено или ТипЗнч(ТекКолонка) <> Тип("ПолеФормы") или ТекКолонка.Вид <> ВидПоляФормы.ПолеВвода или ТекКолонка.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные = Элементы[ТекТаблица.Имя].ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ЭФ = ТекКолонка;
		
		пПоле = Сред(ТекКолонка.Имя, СтрДлина(ТекТаблица.Имя)+2);
		
		пЗначение = ТекДанные[пПоле];
		Если пЗначение = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		пПолноеИмя = вПолучитьПолноеИмяМД(пЗначение);
		Если пПолноеИмя = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		СтрукПараметры = новый Структура("ПутьКФормам, ПолноеИмя, АдресаХранилищ", ПутьКФормам, пПолноеИмя, _АдресаХранилищ);
		СтрукПараметры.Вставить("ТекущаяСтрока", пЗначение);
		СтрукПараметры.Вставить("РежимВыбора", истина);
		Попытка
			Если ложь Тогда
				// ПОЧЕМУ ТО НЕ РАБОТАЕТ (НЕ ВОЗНИКАЕТ СОБЫТИЕ ОБРАБОТКА ВЫБОРА)
				ОткрытьФорму(ПутьКФормам + "ФормаСпискаОбъектов", СтрукПараметры, ЭФ,истина,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			Иначе
				// ПРИШЛОСЬ СДЕЛАТЬ КОСТЫЛЬ
				мОбработкаВыбора = новый Структура("Таблица, Поле", ТекТаблица.Имя, пПоле);
				ОткрытьФорму(ПутьКФормам + "ФормаСпискаОбъектов", СтрукПараметры, ЭтаФорма,истина,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			КонецЕсли;
		Исключение
			пОписаниеОшибки = ОписаниеОшибки();
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораСпец(ВыбранноеЗначение, ИсточникВыбора)
	Если мОбработкаВыбора <> Неопределено Тогда
		Элементы[мОбработкаВыбора.Таблица].ТекущиеДанные[мОбработкаВыбора.Поле] = ВыбранноеЗначение;
		мОбработкаВыбора = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция вПолучитьПолноеИмяМД(Ссыдка)
	Попытка
		Возврат Ссыдка.Метаданные().ПолноеИмя();
	Исключение
		Возврат Неопределено;
	КонецПопытки;
КонецФункции


// ВСТАВКА ССЫЛКИ НА ОБЪЕКТ

&НаСервереБезКонтекста
Функция вНайтиОбъектПоURL(Знач URL)
    Поз1 = Найти(URL, "e1cib/data/");
    Поз2 = Найти(URL, "?ref=");
	
	Если Поз1 = 0 или Поз2 = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
	    ИмяТипа = Сред(URL, Поз1 + 11, Поз2 - Поз1 - 11);
	    ШаблонЗначения = ЗначениеВСтрокуВнутр(ПредопределенноеЗначение(ИмяТипа + ".ПустаяСсылка"));
	    ЗначениеСсылки = СтрЗаменить(ШаблонЗначения, "00000000000000000000000000000000", Сред(URL, Поз2 + 5));
	    Ссылка = ЗначениеИзСтрокиВнутр(ЗначениеСсылки);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Ссылка;
КонецФункции

&НаКлиенте
Процедура _ВставитьСсылкуНаОбъект(Команда)
	Значение = Неопределено;
	пДоступныеТипы = новый ОписаниеТипов;
	
	ЭФ = ЭтаФорма.ТекущийЭлемент;
	Если ЭФ.ТолькоПросмотр или не ЭФ.Доступность Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные = Неопределено;
	пЕстьСсылочныйТип = ложь;
	
	Если ТипЗнч(ЭФ) = Тип("ПолеФормы") Тогда
		пПолеЭФ = ЭФ;
		Если пПолеЭФ.Имя = "мОбъектСсылка" или пПолеЭФ.Имя = "_ЗначениеДляЗаполнения" Тогда
			пИмяПоля = пПолеЭФ.Имя;
			пЕстьСсылочныйТип = истина;
		Иначе
			Возврат;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ЭФ) = Тип("ТаблицаФормы") Тогда
		пПолеЭФ = ЭФ.ТекущийЭлемент;
		Если пПолеЭФ.ТолькоПросмотр или не пПолеЭФ.Доступность Тогда
			Возврат;
		КонецЕсли;
		
		ТекДанные = ЭФ.ТекущиеДанные;
		Если ТекДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		пИмяПоля = Сред(пПолеЭФ.Имя, СтрДлина(Эф.Имя)+2);
	Иначе
		Возврат;
	КонецЕсли;
	
	Если не пЕстьСсылочныйТип Тогда
		пЕстьСсылочныйТип = вОписаниеТиповСодержитСсылочныйТип(пПолеЭФ.ДоступныеТипы);
		пДоступныеТипы = пПолеЭФ.ДоступныеТипы;
	КонецЕсли;
	
	Если пЕстьСсылочныйТип Тогда
		Если не ЗначениеЗаполнено(пИмяПоля) Тогда
			Возврат;
		КонецЕсли;
		
		пСтрук = новый Структура("Таблица");
		пСтрук.Вставить("Имя", пИмяПоля);
		Если ТипЗнч(ЭФ) = Тип("ТаблицаФормы") Тогда
			пСтрук.Таблица = ЭФ.Имя;
		КонецЕсли;
		пСтрук.Вставить("ДоступныеТипы", пДоступныеТипы);
		
		пОповещение = новый ОписаниеОповещения("вОбработатьВводURL", ЭтаФорма, пСтрук);
		ПоказатьВводСтроки(пОповещение,,"Введите ссылку на объект");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура вОбработатьВводURL(пЗначение, пСтрук) Экспорт
	Если пЗначение <> Неопределено Тогда
		пСсылка = вНайтиОбъектПоURL(пЗначение);
		Если пСсылка <> Неопределено Тогда
			Если пСтрук.ДоступныеТипы.Типы().Количество() <> 0 Тогда
				Если не пСтрук.ДоступныеТипы.СодержитТип(ТипЗнч(пСсылка)) Тогда
					ПоказатьПредупреждение(, "Не правильное значение для подстановки!", 20);
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
 			Если ЗначениеЗаполнено(пСтрук.Таблица) Тогда
				ТекДанные = Элементы[пСтрук.Таблица].ТекущиеДанные;
				Если ТекДанные <> Неопределено Тогда
					ТекДанные[пСтрук.Имя] = пСсылка;
				КонецЕсли;
			Иначе
				Если пСтрук.Имя = "мОбъектСсылка" Тогда
					Если СтрНайти(вПолучитьПолноеИмяМД(пСсылка), "Документ.") <> 1 Тогда
						ПоказатьПредупреждение(, "Не правильное значение для подстановки!", 20);
						Возврат;
					КонецЕсли;
					мОбъектСсылка = пСсылка;
					ЭтаФорма.КлючУникальности = мОбъектСсылка;
					
 					_Обновить(Неопределено);
				Иначе
					ЭтаФорма[пСтрук.Имя] = пСсылка;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция вОписаниеТиповСодержитСсылочныйТип(Знач пОписаниеТипов)
	пЕстьСсылочныйТип = истина;
	
	пДоступныеТипы = пОписаниеТипов.Типы();
	
	Если пДоступныеТипы.Количество() <> 0 Тогда
		пЕстьСсылочныйТип = ложь;
		Для каждого Элем из пДоступныеТипы Цикл
			Если мНеСсылочныеТипы.Найти(Элем) = Неопределено Тогда
				пЕстьСсылочныйТип = истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат пЕстьСсылочныйТип;
КонецФункции

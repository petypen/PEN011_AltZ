
&НаСервереБезКонтекста
Функция вСкопироватьСтруктуру(Источник)
	Струк = новый Структура;
	
	Для каждого Элем из Источник Цикл
		Струк.Вставить(Элем.Ключ, Элем.Значение);
	КонецЦикла;
	
	Возврат Струк;
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Заголовок = "Структура специальных таблиц хранения БД";
	
	_ПолноеИмя = Параметры.ПолноеИмя;
	
	ПутьКФормам = Параметры.ПутьКФормам;
	
	_АдресаХранилищ = вСкопироватьСтруктуру(Параметры._АдресаХранилищ);
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.НастройкиОбработки);
	
	Попытка
		пТаблицыСХ = ПолучитьИзВременногоХранилища(_АдресаХранилищ.ТаблицыСХ);
	Исключение
		пТаблицыСХ = -1;
	КонецПопытки;
	
	Если пТаблицыСХ = -1 или пТаблицыСХ.Количество() = 0 Тогда
		Отказ = истина;
	Иначе
		вЗаполнитьРазделСтруктураХранения(пТаблицыСХ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	вПерейтиНаТаблицу();
КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()
	вПерейтиНаТаблицу();
КонецПроцедуры

&НаКлиенте
Процедура вПерейтиНаТаблицу()
	пСтроки = _СХТаблицы.НайтиСтроки(новый Структура("ИмяТаблицыХранения", _ПолноеИмя));
	Если пСтроки.Количество() <> 0 Тогда
		Элементы._СХТаблицы.ТекущаяСтрока = пСтроки[0].ПолучитьИдентификатор();
	КонецЕсли;
КонецПроцедуры



// структура хранения

&НаСервере
Процедура вЗаполнитьРазделСтруктураХранения(Знач ДанныеСХ = Неопределено)
	НомерХ = 0;
	НомерХХ = 0;
	
	пСоотвИмен = новый Соответствие;
	
	Для каждого Стр из ДанныеСХ Цикл
		НомерХ = НомерХ + 1;
		НомерТаблицы = "(" + НомерХ +")";
		
		НС = _СХТаблицы.Добавить();
		ЗаполнитьЗначенияСвойств(НС, Стр);
		НС.НомерТаблицы = НомерТаблицы;
		//Если ПустаяСтрока(НС.ИмяТаблицы) Тогда
		//	НС.ИмяТаблицы = _ПолноеИмя + "(" + Стр.Назначение + ")";
		//КонецЕсли;
		
		Для каждого СтрХ из Стр.Поля Цикл
			НС = _СХПоля.Добавить();
			ЗаполнитьЗначенияСвойств(НС, СтрХ);
			НС.ИмяТаблицыХранения = Стр.ИмяТаблицыХранения;
			НС.НомерТаблицы = НомерТаблицы;
			
			Если не ПустаяСтрока(СтрХ.ИмяПоля) и пСоотвИмен[СтрХ.ИмяПоляХранения] = Неопределено Тогда
				пСоотвИмен.Вставить(СтрХ.ИмяПоляХранения, СтрХ.ИмяПоля);
			КонецЕсли;
		КонецЦикла;
		
		
		Для каждого СтрХ из Стр.Индексы Цикл
			НомерХХ = НомерХХ + 1;
			НомерИндекса = "(" + НомерХХ +")";
			
			НС = _СХИндексы.Добавить();
			ЗаполнитьЗначенияСвойств(НС, СтрХ);
			НС.ИмяТаблицыХранения = Стр.ИмяТаблицыХранения;
			НС.НомерТаблицы = НомерТаблицы;
			НС.НомерИндекса = НомерИндекса;
			
			Для каждого СтрХХ из СтрХ.Поля Цикл
				НС = _СХПоляИндексов.Добавить();
				ЗаполнитьЗначенияСвойств(НС, СтрХХ);
				НС.НомерИндекса = НомерИндекса;
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;
	
	Если пСоотвИмен.Количество() <> 0 Тогда
		Если пСоотвИмен["Account"] = Неопределено Тогда
			пСоотвИмен["Account"] = "Счет";
		КонецЕсли;
		Если пСоотвИмен["_AccountRRef"] = Неопределено Тогда
			пСоотвИмен["_AccountRRef"] = "Счет";
		КонецЕсли;
		
		Для каждого Стр из _СХПоля.НайтиСтроки(новый Структура("ИмяПоля", "")) Цикл
			пЗначение = пСоотвИмен[Стр.ИмяПоляХранения];
			Если пЗначение <> Неопределено Тогда
				Стр.ИмяПоля = пЗначение;
			КонецЕсли;
		КонецЦикла;
		Для каждого Стр из _СХПоляИндексов.НайтиСтроки(новый Структура("ИмяПоля", "")) Цикл
			пЗначение = пСоотвИмен[Стр.ИмяПоляХранения];
			Если пЗначение <> Неопределено Тогда
				Стр.ИмяПоля = пЗначение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _СХТаблицыПриАктивизацииСтроки(Элемент)
	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Элементы._СХПоля.ОтборСтрок = новый ФиксированнаяСтруктура("НомерТаблицы", ТекДанные.НомерТаблицы);
		Элементы._СХИндексы.ОтборСтрок = новый ФиксированнаяСтруктура("НомерТаблицы", ТекДанные.НомерТаблицы);
	Иначе
		Элементы._СХПоля.ОтборСтрок = новый ФиксированнаяСтруктура("НомерТаблицы", "-1");
		Элементы._СХИндексы.ОтборСтрок = новый ФиксированнаяСтруктура("НомерТаблицы", "-1");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _СХИндексыПриАктивизацииСтроки(Элемент)
	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Элементы._СХПоляИндексов.ОтборСтрок = новый ФиксированнаяСтруктура("НомерИндекса", ТекДанные.НомерИндекса);
	Иначе
		Элементы._СХПоляИндексов.ОтборСтрок = новый ФиксированнаяСтруктура("НомерИндекса", "-1");
	КонецЕсли;
КонецПроцедуры
